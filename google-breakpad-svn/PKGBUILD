# Maintainer: Lars Hagstr√∂m <lars@foldspace.nu>
# Contributor: John Schoenick <john@pointysoftware.net>
# Contributor: Geoffrey Teale <tealeg@googlemail.com>
pkgname=google-breakpad-svn
pkgver=1369
pkgrel=2
pkgdesc="An open-source multi-platform crash reporting system"
arch=('i686' 'x86_64' 'armv7h')
url="http://code.google.com/p/google-breakpad/"
license=('BSD')
depends=('curl')
makedepends=('subversion')
options=('staticlibs')

_svntrunk=http://google-breakpad.googlecode.com/svn/trunk
_svnmod=google-breakpad-read-only

build() {
  cd "$srcdir"

  if [ -d "$_svnmod"/.svn ]; then
    (cd "$_svnmod" && svn up -r $pkgver)
  else
    svn co "$_svntrunk" --config-dir ./ -r $pkgver "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"

  rm -rf "$_svnmod-build"
  svn export "$_svnmod" "$_svnmod-build"
  cd "$_svnmod-build"

  #
  # BUILD
  #
  msg2 "Running autotools"
  autoreconf -v --force --install
  msg2 "Building"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}

